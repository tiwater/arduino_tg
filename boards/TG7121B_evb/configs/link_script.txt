MEMORY
{
  RAM (rwx) : ORIGIN = 0x0000, LENGTH = 0xc000
  EM2 (rw): ORIGIN = 0x50006000, LENGTH = 0x2000
  FLASH (rx) : ORIGIN = 0x1800b000, LENGTH = 0x40000
}

SECTIONS
{
    __vector_lma__ = ORIGIN(FLASH);
    
    .vector :
    {
        . = ALIGN(4);
        __vector__start__ = .;
        KEEP(*(.isr_vector))
        __vector__end__ = .;
    }>RAM AT>FLASH

    __data_lma__ = __vector_lma__ + SIZEOF(.vector);
    
    .data :
    {
        . = ALIGN(4);
        __data_start__ = .;
        *(.data*)
        *(.xip_banned)
        _net_buf_pool_list = .;
        KEEP(*(SORT_BY_NAME("._net_buf_pool.static.*")))
        __data_end__ = ALIGN(4);
    }>RAM AT> FLASH   

    .text :
    {
    
        KEEP(*(.init))
        KEEP(*(.fini))

        *crtbegin.o(.ctors)
        *crtbegin?.o(.ctors)
        *(EXCLUDE_FILE(*crtend?.o *crtend.o) .ctors)
        *(SORT(.ctors.*))
        *(.ctors)


        *crtbegin.o(.dtors)
        *crtbegin?.o(.dtors)
        *(EXCLUDE_FILE(*crtend?.o *crtend.o) .dtors)
        *(SORT(.dtors.*))
        *(.dtors)

        *(.eh_frame*)
        *(.text* .rodata*)

        _bt_settings_start = .;
        KEEP(*(._bt_settings.static.*))
        _bt_settings_end = .;

    }>FLASH
    
    .em2_bss (NOLOAD):
    {
        __em2_bss_start__ = .;
        *(.bss.hci_rx_task_stack .bss.g_timer_task_stack .bss.work_q_stack .bss.heap_buf)
        __em2_bss_end__ = .;
    }>EM2

    .bss (NOLOAD):
    {
        __bss_start__ = .;
        *(.bss* COMMON)
        *(SECTION_RTT)
        __bss_end__ = ALIGN(4);
    }>RAM

    .reset_retain MAX(__bss_end__, 0x6AE8) (NOLOAD):
    {
        . = ALIGN(4);
        
        reset_retain_start = .;
        . += 1*4;
        reset_retain_end = .;
    } > RAM
    ASSERT(reset_retain_end <= 0xb800, "Error end address for reset retain")
    
    .heap :
    {
        __heap_start = .;
        *(.heap*)
        __heap_end = .;
    }>RAM

    .stack_dummy __heap_end :
    {
        *(.stack)
        __StackTop = ALIGN(4);
    }>RAM
    ASSERT(__StackTop <= __stack_data_start__,"Error end address for main stack")
}
